apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.uxxu.konashi.inspector.android"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 102000
        versionName "1.2"
    }
    signingConfigs {
        release {
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        def output = variant.outputs.get(0)
        File apk = output.outputFile
        String newName = output.outputFile.name
                .replace('.apk', "-${variant.mergedFlavor.versionName}-${variant.mergedFlavor.versionCode}.apk")
                .replace('app-', "${variant.mergedFlavor.applicationId}-")
        output.outputFile = new File(apk.parentFile, newName)
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.uxxu.konashi:konashi-android-sdk:2.1.0'
    compile "com.github.hotchemi:permissionsdispatcher:2.1.3"
    apt "com.github.hotchemi:permissionsdispatcher-processor:2.1.3"
}

// credentials/release.keystore.properties::
//
//     storeFile={$pathToProject}/credentials/release.keystore
//     storePassword={$storePassword}
//     keyAlias=magnum
//     keyPassword={$keyPassword}
//
File signingPropertiesFile = rootProject.file('credentials/release.keystore.properties')
if (signingPropertiesFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(signingPropertiesFile))
    android.signingConfigs.release.storeFile = file(props.storeFile)
    android.signingConfigs.release.storePassword = props.storePassword
    android.signingConfigs.release.keyAlias = props.keyAlias
    android.signingConfigs.release.keyPassword = props.keyPassword
}